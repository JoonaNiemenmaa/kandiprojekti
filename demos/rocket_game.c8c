sprite rocket = {
	0b00100000,
	0b01110000,
	0b01110000,
	0b11111000,
	0b11011000,
	0b10001000
};

sprite ufo = {
	0b01111110,
	0b11011011,
	0b01111110
};

var ufo_x = 0;
var ufo_y = 4;
var ufo_direction = 1;

var rocket_x = 31;
var rocket_y = 24;

var launched = 0;
var collision = 0;

var score = 0;

var right_limit = 64 - 8 - 5;
var left_limit = 5;

while (score != 10) {

	if (ufo_direction) {
		var ufo_x = ufo_x + 1;
	} else {
		var ufo_x = ufo_x - 1;
	}

	if (ufo_x == left_limit) {
		var ufo_direction = 1;
	}
	if (ufo_x == right_limit) {
		var ufo_direction = 0;
	}

	if (launched == 0) {
		if (pressed(7)) {
			if (rocket_x != left_limit) {
				var rocket_x = rocket_x - 1;
				var draw_rocket = 1;
			}
		}
		if (pressed(9)) {
			if (rocket_x != right_limit) {
				var rocket_x = rocket_x + 1;
				var draw_rocket = 1;
			}
		}
	} else {
		var rocket_y = rocket_y - 1;
		var draw_rocket = 1;

		if (rocket_y == 0) {
			var launched = 0;
			var rocket_y = 24;
		}
	}

	if (pressed(5)) {
		var launched = 1;
	}

	clear;
	draw(ufo, ufo_x, ufo_y);

	var collision = draw(rocket, rocket_x, rocket_y);

	if (collision) {
		var score = score + 1;
		var launched = 0;
		var rocket_y = 24;
	}

	draw_char(score, 59, 26);
}

sprite w = {
	0b10000001,
	0b10011001,
	0b01011010,
	0b01100110,
	0b00100100
};

sprite i = {
	0b11100000,
	0b01000000,
	0b01000000,
	0b01000000,
	0b11100000
};

sprite n = {
	0b10001001,
	0b11001001,
	0b10101001,
	0b10011000,
	0b10001001
};

var offset_x = 21;
var offset_y = 13;

draw(w, offset_x, offset_y);
draw(i, offset_x + 9, offset_y);
draw(n, offset_x + 13, offset_y);
